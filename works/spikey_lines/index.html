<!DOCTYPE html>
<html lang="en" dir="ltr">
    <head>
        <meta charset="utf-8">
        <title>glsl_template</title>
        <style media="screen">
            html, body, canvas {
                overflow: hidden;
                margin: 0;
                padding: 0;
                height: 100%;
                width: 100%;
            }
        </style>
        <script src="main.js" charset="utf-8"></script>
        <script src="three.min.js"></script>


        <script id="vertexShader" type="x-shader/x-vertex">
            precision highp float;
            precision highp int;

            void main() {
                gl_Position = vec4( position, 1.0 );
            }
        </script>


        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            #define LOOP_INT 1
            precision highp float;
            precision highp int;

            uniform vec2  u_resolution;
            uniform float u_time;
            uniform vec2  u_mouse;

            vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
            vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}

            float snoise(vec3 v){
                const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
                const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

                // First corner
                vec3 i  = floor(v + dot(v, C.yyy) );
                vec3 x0 =   v - i + dot(i, C.xxx) ;

                // Other corners
                vec3 g = step(x0.yzx, x0.xyz);
                vec3 l = 1.0 - g;
                vec3 i1 = min( g.xyz, l.zxy );
                vec3 i2 = max( g.xyz, l.zxy );

                //  x0 = x0 - 0. + 0.0 * C
                vec3 x1 = x0 - i1 + 1.0 * C.xxx;
                vec3 x2 = x0 - i2 + 2.0 * C.xxx;
                vec3 x3 = x0 - 1. + 3.0 * C.xxx;

                // Permutations
                i = mod(i, 289.0 );
                vec4 p = permute( permute( permute(
                         i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                       + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
                       + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

                // Gradients
                // ( N*N points uniformly over a square, mapped onto an octahedron.)
                float n_ = 1.0/7.0; // N=7
                vec3  ns = n_ * D.wyz - D.xzx;

                vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

                vec4 x_ = floor(j * ns.z);
                vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

                vec4 x = x_ *ns.x + ns.yyyy;
                vec4 y = y_ *ns.x + ns.yyyy;
                vec4 h = 1.0 - abs(x) - abs(y);

                vec4 b0 = vec4( x.xy, y.xy );
                vec4 b1 = vec4( x.zw, y.zw );

                vec4 s0 = floor(b0)*2.0 + 1.0;
                vec4 s1 = floor(b1)*2.0 + 1.0;
                vec4 sh = -step(h, vec4(0.0));

                vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
                vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

                vec3 p0 = vec3(a0.xy,h.x);
                vec3 p1 = vec3(a0.zw,h.y);
                vec3 p2 = vec3(a1.xy,h.z);
                vec3 p3 = vec3(a1.zw,h.w);

                //Normalise gradients
                vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
                p0 *= norm.x;
                p1 *= norm.y;
                p2 *= norm.z;
                p3 *= norm.w;

                // Mix final noise value
                vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
                m = m * m;
                return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                        dot(p2,x2), dot(p3,x3) ) );
            }

            vec3 permute2(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

            float snoise2(vec2 v){
                const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                       -0.577350269189626, 0.024390243902439);
                vec2 i  = floor(v + dot(v, C.yy) );
                vec2 x0 = v -   i + dot(i, C.xx);
                vec2 i1;
                i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
                vec4 x12 = x0.xyxy + C.xxzz;
                x12.xy -= i1;
                i = mod(i, 289.0);
                vec3 p = permute2( permute2( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));
                vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
                dot(x12.zw,x12.zw)), 0.0);
                m = m*m ;
                m = m*m ;
                vec3 x = 2.0 * fract(p * C.www) - 1.0;
                vec3 h = abs(x) - 0.5;
                vec3 ox = floor(x + 0.5);
                vec3 a0 = x - ox;
                m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
                vec3 g;
                g.x  = a0.x  * x0.x  + h.x  * x0.y;
                g.yz = a0.yz * x12.xz + h.yz * x12.yw;
                return 130.0 * dot(m, g);
            }

            const mat2 m2 = mat2(0.8,-0.6,0.6,0.8);
            float fbm( in vec3 p ){
                float f = 0.0;
                f += 0.5000*snoise( p ); p.xy = m2*p.xy*2.02;
                f += 0.2500*snoise( p ); p.xy = m2*p.xy*2.03;
                f += 0.1250*snoise( p ); p.xy = m2*p.xy*2.01;
                f += 0.0625*snoise( p );

                return f/0.9375;
            }

            float fbm2( in vec2 p ){
                float f = 0.0;
                f += 0.5000*snoise2( p ); p = m2*p*2.02;
                f += 0.2500*snoise2( p ); p = m2*p*2.03;
                f += 0.1250*snoise2( p ); p = m2*p*2.01;
                f += 0.0625*snoise2( p );

                return f/0.9375;
            }

            vec3 textureLookup(vec2 st, sampler2D tex) {
                return texture2D(tex,st).xxx;
            }

            vec2 rotateAroundPoint(vec2 point, vec2 center, float angle) {
                float x = center.x + (point.x-center.x)*cos(angle) - (point.y-center.y)*sin(angle);
                float y = center.y + (point.x-center.x)*sin(angle) + (point.y-center.y)*cos(angle);
                return vec2(x,y);
            }

            vec3 colorLookup(vec2 st, vec2 center, float time) {
                vec3 color = vec3(step(0.,sin(10.*snoise(vec3(st*2.,time)))));
                return color;
            }


            vec2 getSlope(vec2 st, vec2 center, float time, vec2 pixel) {
                vec2 slope = vec2(0.);

                for (int i = -LOOP_INT; i <= LOOP_INT; i++) {
                    for (int j = -LOOP_INT; j <= LOOP_INT; j++) {
                        vec2 curOffset = vec2(float(i),float(j))*pixel;
                        vec2 curPoint = st + curOffset;
                        if (i != j) slope += curOffset * length(colorLookup(curPoint,center,time));
                    }
                }

                return slope;
            }

            float rand(vec2 n){
                return fract(sin(dot(n ,vec2(12.9898,78.233))) * 43758.5453);
            }

            float angleBetween(vec2 v1, vec2 v2) {
                return acos(dot(normalize(v1), normalize(v2))/(length(v1)*length(v2)));
            }

            void main() {
                vec2 st     = gl_FragCoord.xy/u_resolution.xy;
                vec2 ms     = 2. * vec2(u_mouse.x,u_mouse.y)/u_resolution.xy;
                ms.y        = (-ms.y) + 1.;
                vec2 center = vec2(0.5,0.5);

                // now we scale the x coordinate to make the x values to
                // represent the same number of pixels as the y values.
                // if the new x value is x1, and old x value is x0,
                // the following is now true: 0. <= x1 <= x0 * (width/height).

                st.x     *= u_resolution.x/u_resolution.y;
                ms.x     *= u_resolution.x/u_resolution.y;
                center.x *= u_resolution.x/u_resolution.y;

                vec2 pixel = 100./u_resolution.xy;

                float s_time = u_time/20.;

                vec2 randPoint = (vec2(snoise2(vec2(s_time)),snoise2(vec2(s_time+100.)))+1.)/2.;
                randPoint.x *= u_resolution.x/u_resolution.y;

                randPoint = ms;

                st += vec2(
                    rand(vec2(st + sin(u_time*100.))),
                    rand(vec2(st + sin(u_time*100. + 100.)))
                ) * max(0.,distance(randPoint,st)-0.02)/10.;

                vec3 color = vec3(1.);

                color = colorLookup(st,center,s_time);

                vec2 curSlope = getSlope(st,center,s_time,pixel);
                color = colorLookup(st + (10.*(curSlope*snoise2(vec2(angleBetween(vec2(1.,0.),1000.*curSlope))))),center,s_time);

                if (distance(randPoint,st) < 0.025) {
                    color = vec3(1.,0.,0.);
                }

                gl_FragColor = vec4(color,1.);
            }
        </script>

    </head>
    <body>
        <div id="container"></div>
    </body>
</html>
