<!DOCTYPE html>
<html lang="en" dir="ltr">
    <head>
        <meta charset="utf-8">
        <title>glsl_marble</title>
        <style media="screen">
            html, body, canvas {
                overflow: hidden;
                margin: 0;
                padding: 0;
                height: 100%;
                width: 100%;
            }
        </style>
        <script src="main.js" charset="utf-8"></script>
        <script src="three.min.js"></script>


        <script id="vertexShader" type="x-shader/x-vertex">
            precision highp float;
            precision highp int;

            void main() {
                gl_Position = vec4( position, 1.0 );
            }
        </script>


        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            precision highp float;
            precision highp int;

            uniform vec2  u_resolution;
            uniform float u_time;
            uniform float u_seed;
            uniform vec2  u_mouse;

            vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
            vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}

            float snoise(vec3 v){
                const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
                const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

                // First corner
                vec3 i  = floor(v + dot(v, C.yyy) );
                vec3 x0 =   v - i + dot(i, C.xxx) ;

                // Other corners
                vec3 g = step(x0.yzx, x0.xyz);
                vec3 l = 1.0 - g;
                vec3 i1 = min( g.xyz, l.zxy );
                vec3 i2 = max( g.xyz, l.zxy );

                //  x0 = x0 - 0. + 0.0 * C
                vec3 x1 = x0 - i1 + 1.0 * C.xxx;
                vec3 x2 = x0 - i2 + 2.0 * C.xxx;
                vec3 x3 = x0 - 1. + 3.0 * C.xxx;

                // Permutations
                i = mod(i, 289.0 );
                vec4 p = permute( permute( permute(
                         i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                       + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
                       + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

                // Gradients
                // ( N*N points uniformly over a square, mapped onto an octahedron.)
                float n_ = 1.0/7.0; // N=7
                vec3  ns = n_ * D.wyz - D.xzx;

                vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)

                vec4 x_ = floor(j * ns.z);
                vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

                vec4 x = x_ *ns.x + ns.yyyy;
                vec4 y = y_ *ns.x + ns.yyyy;
                vec4 h = 1.0 - abs(x) - abs(y);

                vec4 b0 = vec4( x.xy, y.xy );
                vec4 b1 = vec4( x.zw, y.zw );

                vec4 s0 = floor(b0)*2.0 + 1.0;
                vec4 s1 = floor(b1)*2.0 + 1.0;
                vec4 sh = -step(h, vec4(0.0));

                vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
                vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

                vec3 p0 = vec3(a0.xy,h.x);
                vec3 p1 = vec3(a0.zw,h.y);
                vec3 p2 = vec3(a1.xy,h.z);
                vec3 p3 = vec3(a1.zw,h.w);

                //Normalise gradients
                vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
                p0 *= norm.x;
                p1 *= norm.y;
                p2 *= norm.z;
                p3 *= norm.w;

                // Mix final noise value
                vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
                m = m * m;
                return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                        dot(p2,x2), dot(p3,x3) ) );
            }

            vec2 rotateAroundPoint(vec2 point, vec2 center, float angle) {
                float x = center.x + (point.x-center.x)*cos(angle) - (point.y-center.y)*sin(angle);
                float y = center.y + (point.x-center.x)*sin(angle) + (point.y-center.y)*cos(angle);
                return vec2(x,y);
            }

            const mat2 m2 = mat2(0.8,-0.6,0.6,0.8);
            float fbm( in vec3 p ){
                float f = 0.0;
                f += 0.5000*snoise( p ); p.xy = m2*p.xy*2.02;
                f += 0.2500*snoise( p ); p.xy = m2*p.xy*2.03;
                f += 0.1250*snoise( p ); p.xy = m2*p.xy*2.01;
                f += 0.0625*snoise( p );

                return f/0.9375;
            }

            void main() {
                vec2 st     = gl_FragCoord.xy/u_resolution.xy;
                vec2 ms     = 2. * vec2(u_mouse.x,u_mouse.y)/u_resolution.xy;
                ms.y        = (-ms.y) + 1.;
                vec2 center = vec2(0.5,0.5);

                // now we scale the x coordinate to make the x values to
                // represent the same number of pixels as the y values.
                // if the new x value is x1, and old x value is x0,
                // the following is now true: 0. <= x1 <= x0 * (width/height).

                st.x     *= u_resolution.x/u_resolution.y;
                ms.x     *= u_resolution.x/u_resolution.y;
                center.x *= u_resolution.x/u_resolution.y;

                float c_scale = 1.;
                st       *= c_scale;
                ms       *= c_scale;
                center   *= c_scale;

                float t_scale = 0.01;
                float s_time = u_time * t_scale + u_seed;


                vec2 rotSt = st + vec2(fbm(vec3(st,s_time*5.))) * 5. * (pow((sin(s_time*M_PI*2. - fbm(vec3(st,s_time*10.))/2.)/1.5),2.));
                vec2 newSt = rotateAroundPoint(st,rotSt,fbm(vec3(st,s_time))*50.);

                vec3 color = vec3(
                    fbm(vec3(newSt,s_time-.15)),
                    fbm(vec3(newSt,s_time)),
                    fbm(vec3(newSt,s_time+.15))
                ) + 0.3;

                gl_FragColor = vec4(color,1.);
            }
        </script>
    </head>
    <body>
        <div id="container"></div>
    </body>
</html>
