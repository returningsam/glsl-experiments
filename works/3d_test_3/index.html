<!DOCTYPE html>
<html lang="en" dir="ltr">
    <head>
        <meta charset="utf-8">
        <title>glsl_template</title>
        <style media="screen">
            html, body, canvas {
                overflow: hidden;
                margin: 0;
                padding: 0;
                height: 100%;
                width: 100%;
            }
        </style>
        <script src="three.min.js"></script>
        <script src="OrbitControls.js" charset="utf-8"></script>
        <script src="main.js" charset="utf-8"></script>


        <script id="vertexShader" type="x-shader/x-vertex">
            #define TYPE        	10.0

            attribute vec4 position;
            attribute vec3 normal;

            uniform mat4 projectionMatrix;
            uniform mat4 modelViewMatrix;
            uniform float u_time;

            varying vec3 vNormal;
            varying float offsetHeight;
            varying vec3 vSlope;

            vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
            float permute(float x){return floor(mod(((x*34.0)+1.0)*x, 289.0));}
            vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}
            float taylorInvSqrt(float r){return 1.79284291400159 - 0.85373472095314 * r;}

            vec4 grad4(float j, vec4 ip){
                const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
                vec4 p,s;

                p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
                p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
                s = vec4(lessThan(p, vec4(0.0)));
                p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www;

                return p;
            }

            float snoise(vec4 v){
                const vec2  C = vec2( 0.138196601125010504,  // (5 - sqrt(5))/20  G4
                                      0.309016994374947451); // (sqrt(5) - 1)/4   F4
                // First corner
                vec4 i  = floor(v + dot(v, C.yyyy) );
                vec4 x0 = v -   i + dot(i, C.xxxx);

                // Other corners

                // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
                vec4 i0;

                vec3 isX = step( x0.yzw, x0.xxx );
                vec3 isYZ = step( x0.zww, x0.yyz );
                //  i0.x = dot( isX, vec3( 1.0 ) );
                i0.x = isX.x + isX.y + isX.z;
                i0.yzw = 1.0 - isX;

                //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
                i0.y += isYZ.x + isYZ.y;
                i0.zw += 1.0 - isYZ.xy;

                i0.z += isYZ.z;
                i0.w += 1.0 - isYZ.z;

                // i0 now contains the unique values 0,1,2,3 in each channel
                vec4 i3 = clamp( i0, 0.0, 1.0 );
                vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
                vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

                //  x0 = x0 - 0.0 + 0.0 * C
                vec4 x1 = x0 - i1 + 1.0 * C.xxxx;
                vec4 x2 = x0 - i2 + 2.0 * C.xxxx;
                vec4 x3 = x0 - i3 + 3.0 * C.xxxx;
                vec4 x4 = x0 - 1.0 + 4.0 * C.xxxx;

                // Permutations
                i = mod(i, 289.0);
                float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
                vec4 j1 = permute( permute( permute( permute (
                         i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
                       + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
                       + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
                       + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));
                // Gradients
                // ( 7*7*6 points uniformly over a cube, mapped onto a 4-octahedron.)
                // 7*7*6 = 294, which is close to the ring size 17*17 = 289.

                vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

                vec4 p0 = grad4(j0,   ip);
                vec4 p1 = grad4(j1.x, ip);
                vec4 p2 = grad4(j1.y, ip);
                vec4 p3 = grad4(j1.z, ip);
                vec4 p4 = grad4(j1.w, ip);

                // Normalise gradients
                vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
                p0 *= norm.x;
                p1 *= norm.y;
                p2 *= norm.z;
                p3 *= norm.w;
                p4 *= taylorInvSqrt(dot(p4,p4));

                // Mix contributions from the five corners
                vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
                vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
                m0 = m0 * m0;
                m1 = m1 * m1;
                return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                           + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

            }

            vec3 rotateAroundPoint3d(vec3 point, float angleX, float angleY, float angleZ) {
                mat3 rotX = mat3( 1.,           0.,           0.,
                                  0.,           cos(angleX), -sin(angleX),
                                  0.,           sin(angleX),  cos(angleX));

                mat3 rotY = mat3( cos(angleY),  0.,           sin(angleY),
                                  0.,           1.,           0.,
                                 -sin(angleY),  0.,           cos(angleY));

                mat3 rotZ = mat3( cos(angleZ), -sin(angleZ),  0.,
                                  sin(angleZ),  cos(angleZ),  0.,
                                  0.,           0.,           1.);
                return point*rotX*rotY*rotZ;
            }

            vec3 permute2(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

            float snoise2(vec2 v){
                const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                       -0.577350269189626, 0.024390243902439);
                vec2 i  = floor(v + dot(v, C.yy) );
                vec2 x0 = v -   i + dot(i, C.xx);
                vec2 i1;
                i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
                vec4 x12 = x0.xyxy + C.xxzz;
                x12.xy -= i1;
                i = mod(i, 289.0);
                vec3 p = permute2( permute2( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));
                vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
                dot(x12.zw,x12.zw)), 0.0);
                m = m*m ;
                m = m*m ;
                vec3 x = 2.0 * fract(p * C.www) - 1.0;
                vec3 h = abs(x) - 0.5;
                vec3 ox = floor(x + 0.5);
                vec3 a0 = x - ox;
                m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
                vec3 g;
                g.x  = a0.x  * x0.x  + h.x  * x0.y;
                g.yz = a0.yz * x12.xz + h.yz * x12.yw;
                return 130.0 * dot(m, g);
            }

            float fbm( in vec4 p ){
                mat3 m3 = mat3(snoise2(vec2(u_time/100. + 10.)),snoise2(vec2(u_time/100. + 20.)),snoise2(vec2(u_time/100. + 30.)),
                               snoise2(vec2(u_time/100. - 10.)),snoise2(vec2(u_time/100. - 20.)),snoise2(vec2(u_time/100. - 30.)),
                               snoise2(vec2(u_time/100. + 40.)),snoise2(vec2(u_time/100. + 50.)),snoise2(vec2(u_time/100. + 60.)));
                float f = 0.0;
                f += 0.5000*snoise( p ); p.xyz = m3*p.xyz*2.02;
                f += 0.2500*snoise( p ); p.xyz = m3*p.xyz*2.03;
                f += 0.1250*snoise( p ); p.xyz = m3*p.xyz*2.01;
                f += 0.0625*snoise( p );

                return f/0.9375;
            }

            float getNoise(vec3 position, float time) {
                return fbm(vec4(2.*position,time/10.));
            }

            vec3 getSlope(vec3 position, float time) {
                vec3 slope = vec3(0.);
                float count = 0.;
                for (int i = -1; i <= 1; i++) {
                    for (int j = -1; j <= 1; j++) {
                        for (int k = -1; k <= 1; k++) {
                            if (i != 0 && j != 0 && k != 0) {
                                slope += vec3(float(i),float(j),float(k))*getNoise(position+vec3(float(i),float(j),float(k))/10.,time);
                                count += 1.;
                            }
                        }
                    }
                }
                return slope/count;
            }

            void main () {
                vNormal = normal;

                float s_time = u_time * 0.01;

                vec4 offset = position;

                // offset.xyz  = rotateAroundPoint3d(offset.xyz,getNoise(position.xyz*2.,s_time+0.)*.9,getNoise(position.xyz*2.,s_time+10.)*.9,getNoise(position.xyz*2.,s_time-10.)*.9);

                offsetHeight = getNoise(offset.xyz/30.,s_time/40.)*20.;
                // vSlope = getSlope(offset.xyz/10.,s_time);
                float rotateAngx = snoise(vec4(offset.xyz/50.,s_time/20. + 0.0))/1.8;
                float rotateAngy = snoise(vec4(offset.xyz/50.,s_time/20. + 25.))/1.8;
                float rotateAngz = snoise(vec4(offset.xyz/50.,s_time/20. + 50.))/1.8;
                offset.xyz += (rotateAroundPoint3d(normal,rotateAngx,rotateAngy,rotateAngz) * offsetHeight);

                gl_Position = projectionMatrix * modelViewMatrix * offset;
            }
        </script>


        <script id="fragmentShader" type="x-shader/x-fragment">
            #define M_PI 3.1415926535897932384626433832795
            precision highp float;
            precision highp int;

            uniform mat4 viewMatrix;
            uniform vec3 cameraPosition;

            uniform vec2  u_resolution;
            uniform float u_time;
            uniform vec2  u_mouse;

            varying vec3 vNormal;
            varying float offsetHeight;
            varying vec3 vSlope;

            vec3 permute2(vec3 x) { return mod(((x*34.0)+1.0)*x, 289.0); }

            float snoise2(vec2 v){
                const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                       -0.577350269189626, 0.024390243902439);
                vec2 i  = floor(v + dot(v, C.yy) );
                vec2 x0 = v -   i + dot(i, C.xx);
                vec2 i1;
                i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
                vec4 x12 = x0.xyxy + C.xxzz;
                x12.xy -= i1;
                i = mod(i, 289.0);
                vec3 p = permute2( permute2( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));
                vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
                dot(x12.zw,x12.zw)), 0.0);
                m = m*m ;
                m = m*m ;
                vec3 x = 2.0 * fract(p * C.www) - 1.0;
                vec3 h = abs(x) - 0.5;
                vec3 ox = floor(x + 0.5);
                vec3 a0 = x - ox;
                m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
                vec3 g;
                g.x  = a0.x  * x0.x  + h.x  * x0.y;
                g.yz = a0.yz * x12.xz + h.yz * x12.yw;
                return 130.0 * dot(m, g);
            }

            void main() {
                vec2 st     = gl_FragCoord.xy/u_resolution.xy;
                vec2 ms     = 2. * vec2(u_mouse.x,u_mouse.y)/u_resolution.xy;
                ms.y        = (-ms.y) + 1.;
                vec2 center = vec2(0.5,0.5);

                // now we scale the x coordinate to make the x values to
                // represent the same number of pixels as the y values.
                // if the new x value is x1, and old x value is x0,
                // the following is now true: 0. <= x1 <= x0 * (width/height).

                st.x     *= u_resolution.x/u_resolution.y;
                ms.x     *= u_resolution.x/u_resolution.y;
                center.x *= u_resolution.x/u_resolution.y;

                gl_FragColor = vec4(cross(vec3(offsetHeight/17.) + vec3(
                    snoise2(vec2(u_time/20. + 0.1)),
                    snoise2(vec2(u_time/20. + 0.)),
                    snoise2(vec2(u_time/20. - 0.1))
                )/10., cameraPosition)/10., 1.0);
            }
        </script>


    </head>
    <body>
        <div id="container"></div>
    </body>
</html>
